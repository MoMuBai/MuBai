apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.mob.mubai"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    //直接写在gradle不安全 这里在混淆文件中写入
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        preview {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [app_label: "@string/app_name_debug"]

            applicationIdSuffix ".debug" // 增加APP ID的后缀

            minifyEnabled false //混淆处理

            shrinkResources false  //去除无用资源

            debuggable true  // 保留debug信息

            signingConfig signingConfigs.debug //签名

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 混淆配置
        }

        release {
            manifestPlaceholders = [app_label: "@string/app_name_release"]

            minifyEnabled true

            shrinkResources false

            debuggable false

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }


        preview {
            manifestPlaceholders = [app_label: "@string/app_name_preview"]

            applicationIdSuffix ".preview"

            debuggable true

            minifyEnabled true

            shrinkResources true

            signingConfig signingConfigs.preview

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        lzg {}
        zjy {}
        lzw {}

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //Test Library
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0-rc3'
    compile 'org.mockito:mockito-core:2.2.9'
    //Support Library
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    //Helper Library
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.nineoldandroids:library:2.4.0'
    //Data Library
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.alibaba:fastjson:1.2.22'
    //HTTP Library
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    //RxJava+RxAndroid Library
    compile 'io.reactivex:rxjava:1.2.3'
    compile 'io.reactivex:rxandroid:1.2.1'
    //Retrofit Library
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    //ImageLoader Library
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.facebook.fresco:fresco:0.14.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    //Third Party Library
    compile 'com.yanzhenjie:recyclerview-swipe:1.0.2'
    compile(name: 'refreshAndLoad', ext: 'aar')
    compile project(':pinnedhead')
}

